	/**FERC Form 1 Rules 
**/


RULE-NAME-PREFIX FERC

assert F1.401b.1 satisfied 
$rule_id = rule-name();

if set('1-F','1/3-Q').contains([covered @concept.local-name ='FormType'])
        skip
else
 $sum1 = {@EnergyActivity @MonthAxis=none @@NameOfTransmissionSystemAxis=*}; 
 $addend = sum(list({nonils @EnergyActivity @MonthAxis=* @@NameOfTransmissionSystemAxis=*})#breakdown);
if $addend == none
 	skip
 else
        tolerance_for_decimals($sum1,$addend,$tolerance_factor)  
message 
"The breakdown of the element {$sum1.concept.name} using the axis NameOfTransmissionSystemAxis has a total value of {$addend}. The value of the aggregate amount for {$sum1.concept.name} is equal to {$sum1}.  The disaggregated amounts are {$breakdown.join(' + ')}

Total Element : {$sum1.concept.name}
Total Period : {$sum1.period}
Total Value : {$sum1}

Rule Id:{$rule_id}"
severity error
rule-focus $sum1

assert F1.401b.2 satisfied 
$rule_id = rule-name();

if set('1-F','1/3-Q').contains([covered @concept.local-name ='FormType'])
        skip
else
 $sum1 = {@NonRequiredSalesForResaleEnergy @MonthAxis=none @@NameOfTransmissionSystemAxis=*}; 
 $addend = sum(list({nonils @NonRequiredSalesForResaleEnergy @MonthAxis=* @@NameOfTransmissionSystemAxis=*})#breakdown);
if $addend == none
 	skip
 else
        tolerance_for_decimals($sum1,$addend,$tolerance_factor)  
message 
"The breakdown of the element {$sum1.concept.name} using the axis NameOfTransmissionSystemAxis and axis MonthAxis has a total value of {$addend}. The value of the aggregate amount for {$sum1.concept.name} is equal to {$sum1}.  The disaggregated amounts are {$breakdown.join(' + ')}

Total Element : {$sum1.concept.name}
Total Period : {$sum1.period}
Total Value : {$sum1} 

Rule Id:{$rule_id}"
severity error
rule-focus $sum1

assert F1.401b.3 satisfied 
$rule_id = rule-name();

{@concept = DayOfMonthlyPeak}#dayMonth > 31

message 
"The element {$dayMonth.concept.name} with a value of {$dayMonth} is a day of the month. The value should not exceed 31.

Total Element : {$dayMonth.concept.name}
Total Period : {$dayMonth.period}
Total Value : {$dayMonth} 
Dimensions : {$dayMonth.dimensions.join(',','=')}

Rule Id:{$rule_id}"
severity error
rule-focus $dayMonth

assert F1.401b.4 satisfied 
$rule_id = rule-name();

$HourDay = {@concept = HourOfMonthlyPeak};
$ValidHours = list(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24);
if $HourDay not in $ValidHours 
        true
else
        false

message 
"The element {$HourDay.concept.name} with a value of {$HourDay} is an hour of the day. The value should be a valid hour between 1 and 24.

Total Element : {$HourDay.concept.name}
Total Period : {$HourDay.period}
Total Value : {$HourDay} 
Dimensions : {$HourDay.dimensions.join(',','=')}

Rule Id:{$rule_id}"
severity error
rule-focus $HourDay